plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'kotlin-platform-js'
apply plugin: 'kotlin-dce-js'

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    experimental.coroutines = "enable"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.9"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:0.22.5'
    expectedBy project(":kthings-common")
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

compileKotlin2Js {
    //kotlinOptions.friendModulesDisabled =false
    //kotlinOptions.main = "call"
    //kotlinOptions.metaInfo = true
    kotlinOptions.moduleKind = "umd"
    //kotlinOptions.noStdlib = true
    //kotlinOptions.outputFile
    kotlinOptions.sourceMap = true
    //kotlinOptions.sourceMapEmbedSources
    //kotlinOptions.sourceMapPrefix
    //kotlinOptions.target = "v5"
    //kotlinOptions.typedArrays = true
}

runDceKotlinJs.dceOptions.devMode = false
runDceTestKotlinJs.dceOptions.devMode = true

clean {
    delete file("node_modules")
}

sourceSets.all { sourceSet ->
    tasks.create(sourceSet.getTaskName("unpack", "kotlinJsDependencies")) {
        group = "build"
        description = "Unpacks ${sourceSet.name} dependencies to a format Node.js can consume"

        def outputDir = new File(buildDir, name)
        inputs.property("compileClasspath", sourceSet.compileClasspath)
        outputs.dir(outputDir)

        doLast {
            copy {
                into(outputDir)
                include "*.js"
                sourceSet.compileClasspath.forEach { thisJar ->
                    from zipTree(thisJar)
                }
            }
        }
    }
}

node {
    download = true
    version = "8.11.1"
}

task runJS(dependsOn: [compileKotlin2Js, unpackKotlinJsDependencies, yarn], type: NodeTask) {
    inputs.files sourceSets.main.output
    group = "application"
    description = "Run JS application without optimisation"
    options = ["-r", "source-map-support/register"]
    script = new File(projectDir, "build/classes/kotlin/main/${archivesBaseName}.js")
    execOverrides {
        it.environment("NODE_PATH", new File(buildDir, "unpackKotlinJsDependencies").toString())
    }
}

task runDceJS(dependsOn: [runDceKotlinJs, yarn], type: NodeTask) {
    group = "application"
    description = "Run JS application, after applying dead-code elimination"
    options = ["-r", "source-map-support/register"]
    script = new File(buildDir, "kotlin-js-min/main/${archivesBaseName}.js")
    execOverrides {
        it.environment("NODE_PATH", new File(buildDir, "kotlin-js-min/main").toString())
    }
}
